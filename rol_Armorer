ZVSE2

** trigger for Armorer
**战斗开始，初始化
!?FU(OnBeforeBattleUniversal);
!!re (side:y)/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!VRi^rol_firstStackId_%(side)^:S(side)*20;
  !!VRi^rol_lastStackId_%(side)^:S(side)*20+21;
  !!VRi^rol_armorer_Lv_%(side)^:S-1;[初始化，如无英雄则值为-1]
  !!if&i^battle_hero_%(side)^>(NO_HERO);
    !!HEi^battle_hero_%(side)^:S(SKILL_ARMORER)/?(armorerLv:y);
    !!VRi^rol_armorer_Lv_%(side)^:S(armorerLv);
  !!en;
!!en;

**每回合
!?FU(OnBattleRound);
!!re (side:y)/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!if&i^rol_armorer_Lv_%(side)^>=(SKILL_BASIC);
    !!VR(armorerLv:y):Si^rol_armorer_Lv_%(side)^;

    !!re (stackId:y)/i^rol_firstStackId_%(side)^/i^rol_lastStackId_%(side)^;
      !!BM(stackId):T?(monType:y) N?(stackCurNum:y) B?(stackInitNum:y);
      !!if&(monType)>(NO_MON)/(stackCurNum)>0/(stackInitNum)>0;
        !!VR(stackRemRatio:y):S(stackCurNum)*100:(stackInitNum);[计算堆栈剩余数量占比]

        !!BM(stackId):D?(preDef:y);
        !!if&i^rol_armorer_Lv_%(side)^=(SKILL_BASIC);
          !!VRi^rol_stdRatio^:S45;                             [防御堆栈剩余数量比例]
          !!VRi^rol_block_count^:S4;                           [每回合格挡次数]
          !!VR(reduceSpeed:y):S1;   
          !!VR(raiseDef:y):S(preDef):12+1;
        !!el&i^rol_armorer_Lv_%(side)^=(SKILL_ADVANCED);
          !!VRi^rol_stdRatio^:S40;
          !!VRi^rol_block_count^:S6;
          !!VR(reduceSpeed:y):S1;
          !!VR(raiseDef:y):S(preDef):12+1;
        !!el&i^rol_armorer_Lv_%(side)^=(SKILL_EXPERT);
          !!VRi^rol_stdRatio^:S35;
          !!VRi^rol_block_count^:S8;
          !!VR(reduceSpeed:y):S1;
          !!VR(raiseDef:y):S(preDef):12+1;
        !!en;

        !!MA:X(monType)/?(initMindImmunityFlag:y);
        !!VR(initMindImmunityFlag):&1024;
        !!MA:X(monType)/?(initDefFlag:y);
        !!VR(initDefFlag:y):&134217728;

        !!if&(stackRemRatio)<=i^rol_stdRatio^/(monType)<>5;   [生物非皇家狮鹫]
          !!BM(stackId):Rd-1;                 [本回合反击次数-1]
          !!BM(stackId):Dd(raiseDef);
          !!BM(stackId):S?(speed:y);
          !!VR(speed):-(reduceSpeed) F1/30;
          !!BM(stackId):S(speed);
          !!BM(stackId):Fd|(MON_FLAG_DEFENDING);
        !!el&(stackRemRatio)<=i^rol_stdRatio^/(monType)=5;    [生物为皇家狮鹫]
          !!BM(stackId):R5;                   [本回合反击次数=3次]
          !!BM(stackId):Dd(raiseDef);
          !!BM(stackId):S?(speed:y);
          !!VR(speed):-(reduceSpeed) F1/30;
          !!BM(stackId):S(speed);
          !!BM(stackId):Fd|(MON_FLAG_DEFENDING);
        !!en;

        !!if&(stackRemRatio)<=i^rol_stdRatio^/(initMindImmunityFlag)<>(MON_FLAG_MIND_IMMUNITY);
          !!BM(stackId):Fd|(MON_FLAG_MIND_IMMUNITY);
        !!el&(stackRemRatio)>i^rol_stdRatio^/(initMindImmunityFlag)<>(MON_FLAG_MIND_IMMUNITY);
          !!BM(stackId):Fd~(MON_FLAG_MIND_IMMUNITY);
        !!en;
      !!en;
    !!en;
  !!en;
!!en;

**堆栈伤害结算
!?FU(OnStackToStackDamage);
!#VA(atkStack:x) (defStack:x) (finalDmgConst:x) (finalDmg:x) (basicDmg:x) (dmgBonus:x) (isDistant:x) (distanceArg:x) (isTheoretical:x);

; Check if the defending stack is processed with spell that prevent moving
!!BM(defStack):G(SPELL_BLIND)/?(blindTurns:y)/d G70/?(stoneTurns:y)/d G74/?(paralyzeTurns:y)/d;
!!FU|(blindTurns)=1/(stoneTurns)=1/(paralyzeTurns)=1:E;[失明/石化/麻痹则退出-无法闪避伤害]

!!BM(defStack):T?(monType:y);

!!BM(defStack):I?(side:y);
!!HEi^battle_hero_%(side)^:S(SKILL_ARMORER)/?(armorerLv:y);

!!if&i^battle_hero_%(side)^>(NO_HERO)/(armorerLv)>=(SKILL_BASIC);
  !!BM(defStack):B?(defStackInitNum:y) N?(defStackCurNum:y);
  !!VR(defStackRemRatio:y):S(defStackCurNum)*100:(defStackInitNum);[计算防御堆栈剩余数量占比]
  !!VR(stdChance:y):Si^rol_block_count^*5;      [当次行动的格挡概率]

  !!FU&(isTheoretical):E;

  !!VR(dmgReduction:y):S60;            [格挡后减伤百分比]

  !!VR(random:y):R0/0/99;
  !!if&(random)<(stdChance)/(defStackRemRatio)<=i^rol_stdRatio^;                  
    !!VR(reducedDmg:y):S(finalDmgConst) *(dmgReduction) :100;
    !!VR(newFinalDmg:y):S(finalDmg) -(reducedDmg);
    !!VR(finalDmg)&(finalDmg)>(newFinalDmg):S(newFinalDmg);
    !!VRi^rol_block_count^:-1;

    !!if&i^battle_isVisible^;
      ; Show animation and battle log
      !!BM(defStack):N?(num:y);
      !!VR(isPlural:y):S(num) -1 B;
      !!SN:H^monname^/(monType)/(isPlural)/?(monName:z);
      !!SN:T^es.822.battleLog^/?(battleLog:z)/^mon^/(monName);
      !!MM:S(battleLog);

      // Set up defence animation (by setting defence flag)
      !!BM(defStack):V84 F?(flags:y);
      !!VR(isDefending:y):S(flags) &(MON_FLAG_DEFENDING);

      !!if&(isDefending)=(FALSE);
        !!VRi^rol_822_blockingStack^:S(defStack) +1;
        !!BM(defStack):Fd|(MON_FLAG_DEFENDING);
      !!en;
      !!FU:E;
    !!en;
  !!en;

!!en;


; blocking (?% chance to evade 60% attack damge)
; Skip theoretical (mouse hover)

// Restore defence flag if set for animation
!?FU(ES_OnAfterAttack)&i^battle_isVisible^/i^rol_822_blockingStack^>0;
!!VR(stack:y):Si^rol_822_blockingStack^ -1;
!!BM(stack):Fd~(MON_FLAG_DEFENDING);
!!VRi^rol_822_blockingStack^:S0;


**增加抗魔几率
!?FU(OnMagicCorrectedResistance);
!!MR:N?(stackId:y) F?(resistChance:y);
!!BM(stackId):I?(side:y);
!!BM(stackId):B?(stackInitNum:y) N?(stackCurNum:y);
!!VR(defStackRemRatio:y):S(stackCurNum)*10:(stackInitNum);[计算堆栈剩余数量占比]

!!FU&i^battle_hero_%(side)^=(NO_HERO):E;
; Skip if it is a beneficial spell
!!MR:S?(spell:y);
!!SS(spell):O?(spellType:y);
!!FU&(spellType)>=1:E;
!!FU&i^rol_armorer_Lv_%(side)^:E;
!!FU&(resistChance)>=100:E;

!!if&i^rol_armorer_Lv_%(side)^=(SKILL_BASIC)/(defStackRemRatio)<=i^rol_stdRatio^;
  !!VR(resistChance):+12 F0/100;
!!el&i^rol_armorer_Lv_%(side)^=(SKILL_ADVANCED)/(defStackRemRatio)<=i^rol_stdRatio^;
  !!VR(resistChance):+16 F0/100;
!!el&i^rol_armorer_Lv_%(side)^=(SKILL_EXPERT)/(defStackRemRatio)<=i^rol_stdRatio^;
  !!VR(resistChance):+20 F0/100;
!!en;
  !!MR:F(resistChance);
